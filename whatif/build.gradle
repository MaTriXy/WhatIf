apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'jacoco'
apply from: '../dependencies.gradle'

ext.repository = [
    name       : "whatif",
    description: "WhatIf is kotlin extensions for expressing a single if-else statement, nullable and boolean.",
    websiteUrl : "https://github.com/skydoves/WhatIf",
    scm        : 'scm:git@github.com/skydoves/WhatIf.git'
]

afterEvaluate {
  publishing {
    publications {
      releaseWhatIf(MavenPublication) {
        from components.java

        group = "com.github.skydoves"
        artifactId = repository.name
        version = versions.versionName

        artifact javadocsJar
        artifact(sourceJar)

        pom {
          name = repository.name
          description = repository.description
          url = repository.websiteUrl
          licenses {
            license {
              name = "The Apache License, Version 2.0"
              url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
          }
          developers {
            developer {
              id = "skydoves"
              name = "Jaewoong Eum"
            }
          }
          scm {
            connection = repository.scm
            developerConnection = repository.scm
            url = repository.websiteUrl
          }
        }
      }
    }
  }
}

kotlin {
  explicitApi()
}

task coverageReport(type: JacocoReport, dependsOn: 'test') {
  def reportDirPath = "$buildDir/reports/codeCoverage"

  reports {
    xml.enabled = true
    html.enabled = true

    xml.destination file("$reportDirPath/${project.name}.xml")
    html.destination file("$reportDirPath/${project.name}")
  }
}

dependencies {
  testImplementation "junit:junit:$versions.junit"
}

afterEvaluate {
  tasks.withType(org.jetbrains.dokka.gradle.DokkaTask).configureEach {
    outputDirectory.set(rootProject.file('docs/api'))
  }
}

dokkaHtml.configure {
  dokkaSourceSets {
    named("main") {
      noAndroidSdkLink.set(false)
    }
  }
}

tasks.withType(Javadoc) {
  excludes = ['**/*.kt']
  options.addBooleanOption('Xdoclint:none', true)
}

task sourceJar(type: Jar) {
  archiveClassifier = "sources"
}

task javadocs(type: Javadoc) {
  source = sourceSets.main.java.srcDirs
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
  archiveClassifier.set('javadoc')
  from javadocs.destinationDir
}

apply from: '../spotless.gradle'
apply from: '../publish.gradle'